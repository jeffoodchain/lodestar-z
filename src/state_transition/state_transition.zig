const std = @import("std");
const Allocator = std.mem.Allocator;
const ForkSeq = @import("config").ForkSeq;
const metrics = @import("metrics.zig");
const observeEpochTransitionStep = metrics.observeEpochTransitionStep;
const observeEpochTransition = metrics.observeEpochTransition;
const time = @import("time");

const types = @import("consensus_types");
const preset = @import("preset").preset;

const Slot = types.primitive.Slot.Type;
const CachedBeaconState = @import("cache/state_cache.zig").CachedBeaconState;
const BeaconConfig = @import("config").BeaconConfig;
const AnyBeaconState = @import("fork_types").AnyBeaconState;
const AnySignedBeaconBlock = @import("fork_types").AnySignedBeaconBlock;
const EpochCache = @import("./cache/epoch_cache.zig").EpochCache;
const verifyProposerSignature = @import("./signature_sets/proposer.zig").verifyProposerSignature;
pub const processBlock = @import("./block/process_block.zig").processBlock;
const EpochTransitionCacheOpts = @import("cache/epoch_transition_cache.zig").EpochTransitionCacheOpts;
const EpochTransitionCache = @import("cache/epoch_transition_cache.zig").EpochTransitionCache;
const processEpoch = @import("epoch/process_epoch.zig").processEpoch;
const computeEpochAtSlot = @import("utils/epoch.zig").computeEpochAtSlot;
const processSlot = @import("slot/process_slot.zig").processSlot;
const deinitReusedEpochTransitionCache = @import("cache/epoch_transition_cache.zig").deinitReusedEpochTransitionCache;
const upgradeStateToAltair = @import("slot/upgrade_state_to_altair.zig").upgradeStateToAltair;
const upgradeStateToBellatrix = @import("slot/upgrade_state_to_bellatrix.zig").upgradeStateToBellatrix;
const upgradeStateToCapella = @import("slot/upgrade_state_to_capella.zig").upgradeStateToCapella;
const upgradeStateToDeneb = @import("slot/upgrade_state_to_deneb.zig").upgradeStateToDeneb;
const upgradeStateToElectra = @import("slot/upgrade_state_to_electra.zig").upgradeStateToElectra;
const upgradeStateToFulu = @import("slot/upgrade_state_to_fulu.zig").upgradeStateToFulu;

pub const ExecutionPayloadStatus = enum(u8) {
    pre_merge,
    invalid,
    valid,
};

pub const DataAvailabilityStatus = enum(u8) {
    pre_data,
    out_of_range,
    available,
};

pub const BlockExternalData = struct {
    execution_payload_status: ExecutionPayloadStatus = .valid,
    data_availability_status: DataAvailabilityStatus = .available,
};

pub fn processSlots(
    allocator: std.mem.Allocator,
    io: std.Io,
    cached_state: *CachedBeaconState,
    slot: Slot,
    _: EpochTransitionCacheOpts,
) !void {
    const config = cached_state.config;
    const epoch_cache = cached_state.epoch_cache;
    const state = cached_state.state;

    if (try state.slot() > slot) return error.outdatedSlot;

    while (try state.slot() < slot) {
        try processSlot(cached_state.state);

        const next_slot = try state.slot() + 1;
        if (next_slot % preset.SLOTS_PER_EPOCH == 0) {
            const epoch_transition_timer = time.timestampNow(io);

            var timer = time.timestampNow(io);
            var epoch_transition_cache = try EpochTransitionCache.init(
                allocator,
                io,
                config,
                epoch_cache,
                state,
            );
            defer epoch_transition_cache.deinit(allocator);
            try observeEpochTransitionStep(.{ .step = .before_process_epoch }, @as(u64, @intCast(time.since(io, timer).nanoseconds)));

            switch (state.forkSeq()) {
                inline else => |f| {
                    try processEpoch(
                        f,
                        allocator,
                        io,
                        config,
                        epoch_cache,
                        state.castToFork(f),
                        &epoch_transition_cache,
                    );
                },
            }
            // TODO(bing): registerValidatorStatuses

            try state.setSlot(next_slot);

            timer = time.timestampNow(io);
            try epoch_cache.afterProcessEpoch(state, &epoch_transition_cache);
            try observeEpochTransitionStep(.{ .step = .after_process_epoch }, @as(u64, @intCast(time.since(io, timer).nanoseconds)));
            // state.commit

            const state_epoch = computeEpochAtSlot(next_slot);

            if (state_epoch == config.chain.ALTAIR_FORK_EPOCH) {
                const phase0_state = try state.tryCastToFork(.phase0);
                const upgraded = try upgradeStateToAltair(allocator, config, epoch_cache, phase0_state);
                state.* = .{ .altair = upgraded.inner };
            }
            if (state_epoch == config.chain.BELLATRIX_FORK_EPOCH) {
                const altair_state = try state.tryCastToFork(.altair);
                const upgraded = try upgradeStateToBellatrix(config, epoch_cache, altair_state);
                state.* = .{ .bellatrix = upgraded.inner };
            }
            if (state_epoch == config.chain.CAPELLA_FORK_EPOCH) {
                const bellatrix_state = try state.tryCastToFork(.bellatrix);
                const upgraded = try upgradeStateToCapella(allocator, config, epoch_cache, bellatrix_state);
                state.* = .{ .capella = upgraded.inner };
            }
            if (state_epoch == config.chain.DENEB_FORK_EPOCH) {
                const capella_state = try state.tryCastToFork(.capella);
                const upgraded = try upgradeStateToDeneb(allocator, config, epoch_cache, capella_state);
                state.* = .{ .deneb = upgraded.inner };
            }
            if (state_epoch == config.chain.ELECTRA_FORK_EPOCH) {
                const deneb_state = try state.tryCastToFork(.deneb);
                const upgraded = try upgradeStateToElectra(allocator, config, epoch_cache, deneb_state);
                state.* = .{ .electra = upgraded.inner };
            }
            if (state_epoch == config.chain.FULU_FORK_EPOCH) {
                const electra_state = try state.tryCastToFork(.electra);
                const upgraded = try upgradeStateToFulu(allocator, config, epoch_cache, electra_state);
                state.* = .{ .fulu = upgraded.inner };
            }

            try epoch_cache.finalProcessEpoch(state);
            metrics.state_transition.epoch_transition.observe(time.durationSeconds(time.since(io, epoch_transition_timer)));
        } else {
            try state.setSlot(next_slot);
        }
    }
}

pub const TransitionOpts = struct {
    verify_state_root: bool = true,
    verify_proposer: bool = true,
    /// NOTE: verifying BLS signatures is expensive - make sure to turn this off for tests.
    verify_signatures: bool = true,
    transfer_cache: bool = true,
    block_external_data: BlockExternalData = .{},
};

pub const StateTransitionResult = struct {
    state: AnyBeaconState,
    epoch_cache: *EpochCache,

    pub fn deinit(self: *StateTransitionResult) void {
        self.state.deinit();
        self.epoch_cache.deinit();
    }
};

pub fn stateTransition(
    allocator: std.mem.Allocator,
    io: std.Io,
    cached_state: *CachedBeaconState,
    signed_block: AnySignedBeaconBlock,
    opts: TransitionOpts,
) !*CachedBeaconState {
    const block = signed_block.beaconBlock();
    const block_slot = block.slot();

    var post_cached_state = try cached_state.clone(
        allocator,
        .{ .transfer_cache = opts.transfer_cache },
    );
    errdefer {
        post_cached_state.deinit();
        allocator.destroy(post_cached_state);
    }

    try metrics.state_transition.onStateClone(post_cached_state, .state_transition);

    try processSlots(
        allocator,
        io,
        post_cached_state,
        block_slot,
        .{},
    );

    const config = post_cached_state.config;
    const post_epoch_cache = post_cached_state.epoch_cache;
    const post_state = post_cached_state.state;

    // Verify proposer signature only
    if (opts.verify_proposer and !try verifyProposerSignature(
        allocator,
        config,
        post_epoch_cache,
        signed_block,
    )) {
        return error.InvalidBlockSignature;
    }

    if (block.forkSeq() != post_state.forkSeq()) {
        return error.InvalidBlockForkForState;
    }
    // Note: time only on success
    var timer = time.timestampNow(io);
    switch (post_state.forkSeq()) {
        inline else => |f| {
            switch (block.blockType()) {
                inline else => |bt| {
                    if (comptime (bt == .blinded and f.lt(.bellatrix)) or (bt == .blinded and f.gte(.gloas))) {
                        return error.InvalidBlockTypeForFork;
                    } else {
                        try processBlock(
                            f,
                            allocator,
                            config,
                            post_epoch_cache,
                            post_state.castToFork(f),
                            &post_cached_state.slashings_cache,
                            bt,
                            block.castToFork(bt, f),
                            opts.block_external_data,
                            .{ .verify_signature = opts.verify_signatures },
                        );
                    }
                },
            }
        },
    }
    metrics.state_transition.process_block.observe(time.durationSeconds(time.since(io, timer)));

    //
    // TODO(bing): commit
    //  const processBlockCommitTimer = metrics?.processBlockCommitTime.startTimer();
    //  postState.commit();
    //  processBlockCommitTimer?.();

    try metrics.state_transition.onPostState(post_cached_state);

    // Verify state root
    if (opts.verify_state_root) {
        timer = time.timestampNow(io);
        const post_state_root = try post_state.hashTreeRoot();
        try metrics.state_transition.state_hash_tree_root.observe(.{ .source = .block_transition }, time.durationSeconds(time.since(io, timer)));

        const block_state_root = block.stateRoot();
        if (!std.mem.eql(u8, post_state_root, block_state_root)) {
            return error.InvalidStateRoot;
        }
    } else {
        // Even if we don't verify the state_root, commit the tree changes
        try post_state.commit();
    }

    return post_cached_state;
}

pub fn deinitStateTransition(io: std.Io) void {
    deinitReusedEpochTransitionCache(io);
}

const TestCase = struct {
    transition_opt: TransitionOpts,
    expect_error: bool,
};

const TestCachedBeaconState = @import("test_utils/root.zig").TestCachedBeaconState;
const generateElectraBlock = @import("test_utils/generate_block.zig").generateElectraBlock;
const testing = std.testing;
const Node = @import("persistent_merkle_tree").Node;

test "state transition - electra block" {
    const test_cases = [_]TestCase{
        .{ .transition_opt = .{}, .expect_error = true },
        .{ .transition_opt = .{ .verify_signatures = false, .verify_proposer = true }, .expect_error = true },
        .{ .transition_opt = .{ .verify_signatures = false, .verify_proposer = false, .verify_state_root = true }, .expect_error = true },
        // this runs through epoch transition + process block without verifications
        .{ .transition_opt = .{ .verify_signatures = false, .verify_proposer = false, .verify_state_root = false }, .expect_error = false },
    };

    inline for (test_cases) |tc| {
        const allocator = std.testing.allocator;
        const pool_size = 256 * 5;
        var pool = try Node.Pool.init(allocator, pool_size);
        defer pool.deinit();

        var test_state = try TestCachedBeaconState.init(allocator, &pool, 256);
        defer test_state.deinit();

        var electra_block = types.electra.SignedBeaconBlock.default_value;
        try generateElectraBlock(allocator, test_state.cached_state, &electra_block);
        defer types.electra.SignedBeaconBlock.deinit(allocator, &electra_block);

        const signed_beacon_block = AnySignedBeaconBlock{ .full_electra = &electra_block };

        // this returns the error so no need to handle returned post_state
        // TODO: if blst can publish BlstError.BadEncoding, can just use testing.expectError
        // testing.expectError(blst.c.BLST_BAD_ENCODING, stateTransition(allocator, test_state.cached_state, signed_block, .{ .verify_signatures = true }));
        const res = stateTransition(
            allocator,
            std.testing.io,
            test_state.cached_state,
            signed_beacon_block,
            tc.transition_opt,
        );
        if (tc.expect_error) {
            if (res) |_| {
                try testing.expect(false);
            } else |_| {}
        } else {
            if (res) |post_state| {
                defer {
                    post_state.deinit();
                    allocator.destroy(post_state);
                }
            } else |_| {
                try testing.expect(false);
            }
        }
    }

    defer deinitStateTransition(std.testing.io);
}

test "state transition - a rejected block leaves the pre-state unchanged" {
    const allocator = std.testing.allocator;
    var pool = try Node.Pool.init(allocator, 256 * 5);
    defer pool.deinit();
    defer deinitStateTransition(std.testing.io);

    var test_state = try TestCachedBeaconState.init(allocator, &pool, 256);
    defer test_state.deinit();

    var electra_block = types.electra.SignedBeaconBlock.default_value;
    try generateElectraBlock(allocator, test_state.cached_state, &electra_block);
    defer types.electra.SignedBeaconBlock.deinit(allocator, &electra_block);

    const signed_beacon_block = AnySignedBeaconBlock{ .full_electra = &electra_block };

    // Snapshot the pre-state just before the transition.
    const before = (try test_state.cached_state.state.hashTreeRoot()).*;
    const before_slot = try test_state.cached_state.state.slot();

    // Full verification rejects this block (it isn't validly signed). stateTransition advances
    // and mutates a clone, then discards it on error — so the original state must come out
    // untouched: same root, same slot. (This is the invariant behind the "mutate then reject"
    // findings; the mutations only ever land on the thrown-away clone.)
    const res = stateTransition(allocator, std.testing.io, test_state.cached_state, signed_beacon_block, .{});
    if (res) |post| {
        post.deinit();
        allocator.destroy(post);
        try testing.expect(false); // expected the block to be rejected
    } else |_| {}

    const after = (try test_state.cached_state.state.hashTreeRoot()).*;
    try testing.expectEqualSlices(u8, &before, &after);
    try testing.expectEqual(before_slot, try test_state.cached_state.state.slot());
}
